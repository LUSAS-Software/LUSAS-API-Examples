' LUSAS API (LPI) PYTHON EXAMPLES
' (https://github.com/LUSAS-Software/LUSAS-API-Examples/)
'
' Example:      04a_Create_and_assign_mesh.lvb
' Description:  Creates and assigns meshes
' Author:       Finite Element Analysis Ltd
' Date:         2025-04-28
' 
' Libraries: Load the Helpers.vbs which contains useful functions
$INCLUDE %RunningScriptPath%\shared\Helpers.vbs
$ENGINE=VBScript

' Check if a model is open
If not existsDatabase() Then
    Call Err.Raise(vbObjectError + 10, "Example Script", vbCrLf & "A model must be open before running this code" & vbCrLf)
End If

'#####################################################
'## Create line and surface to use in this example

' Create line
Set line1 = create_line_by_coordinates(0.0, 3.0, 0.0, 3.0, 3.0, 0.0)
' To get existing line, use the following command instead:
' Set line1 = database.getObject("line", 1) 'get line with ID 1

' Create vertical surface
sweepVector = Array(0, 0, 1)
surfaces1 = sweep_lines(Array(line1), sweepVector)
' To get existing surface, use the following command instead:
' Set surface1 = database.getObject("surface", 1) 'get surface with ID 1


'#####################################################
'# Create Line (beam) mesh
Set lineMeshAttr = database.createMeshLine("Beam Mesh")

' Set the element type and size of elements BMI21 = 2Node thick beam elements, 1 = 1m length (set model units)
Call lineMeshAttr.setSize("BMI21", 1)

' Assign the mesh to the line on loadcase 1
Call lineMeshAttr.assignTo(line1, 1)


'#####################################################
'# Create Surface (shell) mesh
Set surfMeshAttr = database.createMeshSurface("Shell Mesh")
' Set the element type and divisions of elements QTS4 = 4Node quadrilateral surface elements
' 0 divisions in the u and v directions for automatic meshing
Call surfMeshAttr.setRegular("QTS4", 0, 0, True)

' Assign the mesh to the surface on loadcase 1
Call surfMeshAttr.assignTo(surfaces1, 1)


'#####################################################
' Update the mesh to apply the changes
Call database.updateMesh()
