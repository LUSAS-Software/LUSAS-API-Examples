' LUSAS API (LPI) EXAMPLES
' (https://github.com/LUSAS-Software/LUSAS-API-Examples/)
'
' Example:      13_Create_and_assign_supports.lvb
' Description:  Creates and assigns supports
' Author:       Finite Element Analysis Ltd
' Date:         2025-04-28
' 
' Libraries: Load the Helpers.vbs which contains useful functions
$INCLUDE %RunningScriptPath%\shared\Helpers.vbs
$ENGINE=VBScript

' Check if a model is open
If not existsDatabase() Then
    Call Err.Raise(vbObjectError + 10, "Example Script", vbCrLf & "A model must be open before running this code" & vbCrLf)
End If

'#####################################################
'## Create slab and columns to use in this example

' Create slab-surface
xs = Array(0, 5, 5, 0)
ys = Array(0, 0, 3, 3)
zs = Array(0, 0, 0, 0)
Set surface1 = create_surface_by_coordinates(xs, ys, zs)

' Create columns-lines
surfPoints = newObjectSet().add(surface1).addLOF("Points").getObjects("Point")
sweepVector = Array(0, 0, 3)
lines = sweep_points(surfPoints, sweepVector)
' Get the top points of the columns
Dim topPoints()
ReDim topPoints(UBound(lines))
Dim i
For i = 0 To UBound(lines)
    Set topPoints(i) = lines(i).getEndPoint()
Next


'#####################################################
'# Create Fixed support

Set attr = database.createSupportStructural("Fixed")
' Restrict all degrees of freedom
Call attr.setStructural("R", "R", "R", "R", "R", "R") ' R=Restrained

' Assign on slab points and loadcase 1
Call attr.assignTo(surfPoints, 1)


'#####################################################
'# Create pinned support

Set attr = database.createSupportStructural("Pinned")
' Release all rotational degrees of freedom
Call attr.setStructural("R", "R", "R", "F", "F", "F") ' F=Free, R=Restrained

' Assign on column top points and loadcase 1
Call attr.assignTo(topPoints, 1)


'#####################################################
'# Create translation springs support

Set attr = database.createSupportStructural("Springs")
' Mark translational degrees of freedoms as strings
Call attr.setStructural("S", "S", "S", "F", "F", "F")
' Set springs stiffness (in model units)
sType = "Area" ' Spring stiffness distribution: "Total" for total stiffness, "Length" for length distribution, "Area" for area distribution
Call attr.setSpring(sType, 200, 200, 200, 0, 0, 0, 0, 0, 0)

' Assign on surface and loadcase 1
Call attr.assignTo(surface1, 1)


'#####################################################
'# Create/Assign mesh so that supports are visualised

Call database.createMeshLine("Dummy Line Mesh").setSize("BMI21", 1).assignTo(lines, 1)
Call database.createMeshSurface("Dummy Surface Mesh").setRegular("QTS4", 0, 0, True).assignTo(surface1, 1)
Call database.updateMesh()
